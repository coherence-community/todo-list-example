/*
 * Copyright (c) 2021 Oracle and/or its affiliates.
 * Licensed under the Universal Permissive License v 1.0 as shown at
 * https://oss.oracle.com/licenses/upl.
 */

plugins {
  id "application"
  id "java"
  id "com.github.johnrengelman.shadow" version "4.0.2"
  id "com.github.node-gradle.node"     version "3.1.1"
  id "com.google.cloud.tools.jib"      version "3.1.4"
}

repositories {
  mavenLocal()
  maven {
    url = 'https://repo.maven.apache.org/maven2'
    url = "https://plugins.gradle.org/m2/"
  }
}

dependencies {
  annotationProcessor platform("io.micronaut:micronaut-bom:${micronautVersion}")
  annotationProcessor "io.micronaut:micronaut-inject-java"
  annotationProcessor "io.micronaut:micronaut-validation"
  annotationProcessor "io.micronaut.coherence:micronaut-coherence-data:${micronautCohVersion}"

  implementation platform("io.micronaut:micronaut-bom:${micronautVersion}")
  implementation "io.micronaut:micronaut-inject"
  implementation "io.swagger.core.v3:swagger-annotations"
  implementation "io.micronaut:micronaut-validation"
  implementation "io.micronaut:micronaut-runtime"
  implementation "io.micronaut:micronaut-http-server-netty"
  implementation "com.oracle.coherence.ce:coherence:${projectVersion}"
  implementation "io.micronaut.coherence:micronaut-coherence:${micronautCohVersion}"
  implementation "io.micronaut.coherence:micronaut-coherence-data:${micronautCohVersion}"
  implementation "io.reactivex.rxjava2:rxjava:${rxJava2Version}"
  implementation "io.micronaut.graphql:micronaut-graphql:${micronautGQLVersion}"

  runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

  testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
  testAnnotationProcessor "io.micronaut:micronaut-inject-java"

  testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "io.micronaut.test:micronaut-test-junit5"
  testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
  testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"

  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

group = "com.oracle.coherence.examples"
version = "${projectVersion}"

tasks.withType(JavaCompile) {
  dependsOn npmInstall // ensure our react webapp is built
  options.encoding = "UTF-8"
}

ext {
  javaMainClass = "com.oracle.coherence.examples.todo.server.Application"
}

test {
  dependsOn "copyWebappFiles"
  useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

// Copies the react webapp to a build location to be included in the JAR
task copyWebappFiles(type: Copy) {
  from "${project.projectDir}/src/main/web/react/build"
  into "${buildDir}/resources/main/web"
}

// Builds our react webapp
node {
  nodeProjectDir = file("${project.projectDir}/src/main/web/react")
}

jib {
  containerizingMode = "packaged"
  from {
    image = "gcr.io/distroless/java:11"
  }
  to {
    image = "ghcr.io/coherence-community/${rootProject.name}"
    tags = ["${version}"]
  }
  container {
    ports = ["1408", "3000", "9612"]
    jvmFlags = ["-Dcoherence.metrics.http.enabled=true"]
  }
}

application {
  mainClassName = javaMainClass
  applicationDefaultJvmArgs = ["-Dcoherence.metrics.http.enabled=true"]
}

jar {
  dependsOn copyWebappFiles
  manifest {
    attributes(
            "Main-Class": javaMainClass
    )
  }
}

shadowJar {
  dependsOn copyWebappFiles
  mergeServiceFiles()
}
